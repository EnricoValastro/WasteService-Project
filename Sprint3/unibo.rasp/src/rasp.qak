System rasp

/* Messages for Led */
Dispatch blink : blink(_)
Dispatch turnon : turnon(_)
Dispatch turnoff : turnoff(_)

/* Messages for Sonar */
Dispatch sonaractivate   : info(ARG)
Dispatch sonardeactivate : info(ARG)
Event    sonar           : distance( V )   			//emitted by sonardatasource	
Event    sonardata 		 : distance( V )   			//for the application level

/* Messages for Advanced Testing Mode */
Request produce : produce(VAL)
Reply produceok : produceok(_)

Context ctxwasteservice ip [host="192.168.1.4" port=8055]
Context ctxpi ip [host="localhost" port=8065 ]


CodedQActor sonardatasource context ctxpi className "sonarSupport2022" 
CodedQActor datacleaner    	context ctxpi className "dataCleaner"

//CodedQActor datalogger     context ctxpi className "dataLogger"
//CodedQActor distancefilter context ctxpi className "distanceFilter"


QActor led context ctxpi {
	[# 
		var led : `it.unibo`.radarSystem22.domain.interfaces.ILed? = null
		var flagBlink = false
	#]
	State s0 initial{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	starting...", unibo.comm22.utils.ColorsOut.YELLOW)#]
		[#
			led = utility.LedFactory.createLed()
			led!!.turnOff()
		#]
		
	}Goto idle
	
	State idle {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	waiting...", unibo.comm22.utils.ColorsOut.YELLOW)#]
	}Transition t0 whenMsg blink -> blinkon
					whenMsg turnon -> on
					whenMsg turnoff -> off
	State on {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	TurnOn Led", unibo.comm22.utils.ColorsOut.YELLOW)#]
		[#led!!.turnOn()#]
		[# flagBlink = false #]
	}Goto idle
	
	State off {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	TurnOff Led", unibo.comm22.utils.ColorsOut.YELLOW)#]
		[#led!!.turnOff()#]
		[# flagBlink = false #]
	}Goto idle
	
	State blinkon {
		if[# flagBlink == false#]{
			[# unibo.comm22.utils.ColorsOut.outappl("$name	|	Blink Led", unibo.comm22.utils.ColorsOut.YELLOW)#]
			[# flagBlink = true #]
		}
		[#led!!.turnOn()#]
	}Transition t0 whenTime 300 -> blinkoff
					whenMsg turnon -> on
					whenMsg turnoff -> off
	
	State blinkoff {
		[#led!!.turnOff()#]
	}Transition t0 whenTime 300 -> blinkon
					whenMsg turnon -> on
					whenMsg turnoff -> off
}

QActor sonarqak22varesi context ctxpi{
	[# 
	   	val sonarActorName = "sonarqak22varesi"
	   	var advancedTestingMode = false
	   	var VALUE = 0
	#]
	State s0 initial {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	starting...", unibo.comm22.utils.ColorsOut.YELLOW)#]
		run  utility.configureTheSonar(sonarActorName) 
		[# advancedTestingMode = utility.AdvancedTestingModeSetUp.checkForTestingMode() #]	
	}
	Goto advancedTestingMode if[# advancedTestingMode #]
				else sonarActivate
				
	State sonarActivate{
		forward sonarqak22varesi -m sonaractivate : info(ok)
	}			
	Transition t0 whenMsg sonaractivate   -> activateTheSonar
			      whenMsg sonardeactivate -> deactivateTheSonar
	
	State advancedTestingMode{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	ADVANCED TESTING MODE: ON", unibo.comm22.utils.ColorsOut.YELLOW)#]
	}Goto waitForRequest
	State waitForRequest{
		
	}Transition t0 whenRequest produce -> produceData
	State produceData{
		onMsg(produce : produce(VAL)){
			[# VALUE = payloadArg(0).toInt() #]
		}
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	ADVANCED TESTING MODE: producing $VALUE", unibo.comm22.utils.ColorsOut.YELLOW)#]
		emit sonardata : distance($VALUE)
		replyTo produce with produceok : produceok(_)
	}
	
	
	State activateTheSonar{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	waiting...", unibo.comm22.utils.ColorsOut.YELLOW)#]
		forward sonardatasource -m sonaractivate : info(ok)  
	}
	Transition t0  whenEvent sonar         		-> handleSonarData  
	               whenMsg 	 sonardeactivate 	-> deactivateTheSonar
	
	State deactivateTheSonar{
		forward sonardatasource -m sonardeactivate : info(ko)
	}
	Goto end
 
  	State handleSonarData{
  		 onMsg( sonar : distance(D) ){
   		 	[# val D = payloadArg(0) #] 
   		 	[# unibo.comm22.utils.ColorsOut.outappl("$name	|	emitting: ${D}", unibo.comm22.utils.ColorsOut.YELLOW)#] 
  		 	emit sonardata : distance($D) //for the application
  		 }
  	}
   	Transition t0 whenEvent sonar -> handleSonarData
   				  whenMsg sonardeactivate -> deactivateTheSonar
   		
	State end{ 
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	BYE", unibo.comm22.utils.ColorsOut.YELLOW)#]
		[# System.exit(0) #]
	}
}



