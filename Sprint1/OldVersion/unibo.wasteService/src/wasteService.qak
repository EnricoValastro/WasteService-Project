System wasteservice

/* ShutDown Message */
Dispatch exit 	 : exit(_)

/* ContainerManager Messages */
Request  evalreq : evalreq(MAT,QUA) 			// MAT = PLASTIC | GLASS , QUA = Double
Reply 	 evalok  : evalok(_)
Reply 	 evalko  : evalko(_)
Dispatch update  : update(MAT,QUA)

/* Deposit Request Messages */
Request  storewaste   : storewaste(MAT, QUA)
Reply 	 loadaccept   : loadaccept(_)
Reply 	 loadrejected : loadrejected(_)
Dispatch leaveindoor  : leaveindoor(_)

/* WasteServiceCore Message */
Dispatch dojob : dojob(MAT)

/* Deposit Action Message */
Request  pickup 	 : pickup(_)
Reply    pickupdone  : pickupdone(_)
Dispatch dropout     : dropout(MAT)
Event	 dropoutdone : dropoutdone(_)

/* BackHome Message */
Dispatch gotohome 	 : gotohome(_)

Context ctxtransporttrolley ip [host="127.0.0.1" port=8056]
//Context ctxtruck ip [host="127.0.0.1" port=8066]
Context ctxwasteservice ip [host="localhost" port=8055]

ExternalQActor transporttrolleycore context ctxtransporttrolley
//ExternalQActor smartdevice context ctxtruck

/* *********************************************************************************************
 * ---------------------------------------------------------------------------------------------
 * Component addicted to manage container state
 * ---------------------------------------------------------------------------------------------
 **********************************************************************************************/


QActor containermanager context ctxwasteservice{
	[#
		val boxState  = wasteservice.state.ServiceAreaState()
		lateinit var requestMaterialToStore : wasteservice.state.Material 
		var requestWeightToStore : Double = 0.0	
	#]
	
	State init initial {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	starting...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		updateResource[#boxState.toJsonString()#]
	}Goto idle
	
	State idle {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	waiting...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
	}Transition t0 whenRequest evalreq -> evaluation
					whenMsg    update  -> update
					whenMsg    exit    -> end
	
	State evaluation {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	evaluating request...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		onMsg (evalreq : evalreq(MAT,QUA)){
			[#	try{
					requestMaterialToStore = wasteservice.state.Material.valueOf(payloadArg(0).trim().uppercase())  
					requestWeightToStore = payloadArg(1).toDouble()		
				}catch(e : Exception){
					//TobeDone
				}
			#]
		}
			
		if [# boxState.canStore(requestMaterialToStore, requestWeightToStore)#]{
			replyTo evalreq with evalok : evalok(_)
		}
		else {
			replyTo evalreq with evalko : evalko(_)
		}
		
	}Goto idle
	
	State update {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	updating container state", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		onMsg (update : update(MAT,QUA)){
			[#	
				try{
					requestMaterialToStore = wasteservice.state.Material.valueOf(payloadArg(0).trim().uppercase())  
					requestWeightToStore = payloadArg(1).toDouble()	
					boxState.updateBoxWeight(requestMaterialToStore, requestWeightToStore)
				}
				
			#]
			[#
				catch(e : Exception){
					//TobeDone
				}
			#]
		}
		updateResource[#boxState.toJsonString()#]
	}Goto idle
	
	State end {
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	bye", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		terminate 0
	}
}


/* *********************************************************************************************
 * ---------------------------------------------------------------------------------------------
 * Component addicted to handle request from truck driver 
 * ---------------------------------------------------------------------------------------------
 **********************************************************************************************/


QActor wasteservicehandler context ctxwasteservice{
	[#
		lateinit var REQMATERIAL : wasteservice.state.Material 
		var REQWEIGHT : Double = 0.0
	#]
	State init initial{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	starting...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		request wasteservicehandler -m storewaste : storewaste(GLASS, 300)
		request wasteservicehandler -m storewaste : storewaste(PLASTIC, 200)
		
	}Goto idle
	
	State idle{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	waiting...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
	}Transition t0 whenRequest storewaste -> evalReq
					whenMsg    exit 	  -> end
	
	State evalReq{
	
		onMsg (storewaste : storewaste (MAT, QUA)){
		[#
			REQMATERIAL = wasteservice.state.Material.valueOf(payloadArg(0).trim().uppercase())
			REQWEIGHT = payloadArg(1).toDouble()
		#]
		request containermanager -m evalreq : evalreq($REQMATERIAL, $REQWEIGHT)
		}
	}Transition t0 whenReply  evalok -> acceptRequest
					whenReply evalko -> rejectRequest
	
	State acceptRequest{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	request accepted", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		forward containermanager -m update : update($REQMATERIAL, $REQWEIGHT)
		replyTo storewaste with loadaccept : loadaccept(_)
		forward wasteservicecore -m dojob: dojob($REQMATERIAL)		
	}Goto idle
	
	State rejectRequest{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	request rejected", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		forward containermanager -m update : update($REQMATERIAL, 0)
		replyTo storewaste with loadrejected : loadrejected(_)
	}Goto idle
	
	State end{
		terminate 0
	}
	
}


/* *********************************************************************************************
 * ---------------------------------------------------------------------------------------------
 * Core component addicted to manage all the request exec by interacting with TT component 
 * ---------------------------------------------------------------------------------------------
 **********************************************************************************************/


QActor wasteservicecore context ctxwasteservice{
	
	[#
		lateinit var REQMATERIAL : wasteservice.state.Material 
	#]
	
	State init initial{
		[#
			utility.Banner.printBannerWasteService()
		#]
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	starting...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
	}Goto idle

	State idle{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	waiting...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
	}Transition t0 whenMsg 	  dojob -> pickup
					whenEvent dropoutdone -> backHome
					whenMsg   exit -> end

	State pickup{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	asking for pickup", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		onMsg(dojob : dojob(MAT)){
		[#
			try{
			REQMATERIAL = wasteservice.state.Material.valueOf(payloadArg(0).trim().uppercase())
			}catch(e : Exception){}
		#]
		}
		request transporttrolleycore -m pickup : pickup(_)
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	waiting for pickup done...", unibo.comm22.utils.ColorsOut.CYAN)#]
		
	}Transition t0 whenReply pickupdone  -> dropout 
					
	State dropout{
		//forward smartdevice -m leaveindoor : leaveindoor(_)
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	asking for dropout", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		forward transporttrolleycore -m dropout : dropout($REQMATERIAL)
	}Goto idle
	
	State backHome{
		[# unibo.comm22.utils.ColorsOut.outappl("$name	|	sending robot to home", unibo.comm22.utils.ColorsOut.CYAN)#]
		
		forward transporttrolleycore -m gotohome : gotohome(_)
	}Goto idle
	
	State end{
		forward wasteservicehandler -m exit : exit(_)
		forward containermanager -m exit : exit(_)
		terminate 0
	}
}